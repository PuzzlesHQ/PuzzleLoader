import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.puzzle.buildsrx.GameScanner
import com.github.puzzle.buildsrx.GenericTransformer
import com.github.puzzle.buildsrx.transformers.ApiClassTransformer
import com.github.puzzle.buildsrx.transformers.DefaultClassTransformer
import com.github.puzzle.buildsrx.transformers.RedirectClassTransformer
import com.github.puzzle.buildsrx.transformers.ClassPreProcessor
import com.github.puzzle.buildsrx.transformers.RemoveClassTransformer
import org.gradle.api.internal.artifacts.configurations.DefaultConfigurationContainer

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta8'
}

sourceSets {
    api
    backend_0_3_26
    commonBackend
}

group = 'com.github'
version = '69.69.69'

repositories {
    mavenCentral()
    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io'
    }
}

def setupConfiguration(Configuration configuration, boolean isCompile) {
    configurations.getAsMap().keySet().forEach {
        if (it.contains("backend_") || it.contains("commonBackend")) {
            if (!isCompile && !it.contains("Compile")) {
                configurations.getByName((String) it).extendsFrom(configuration)
            } else if (isCompile && !it.contains("Implementation")) {
                configurations.getByName((String) it).extendsFrom(configuration)
            }
        }
    }
}

def setupConfiguration2(Configuration configuration, boolean isCompile) {
    configurations.getAsMap().keySet().forEach {
        if (it.contains("backend_")) {
            if (!isCompile && !it.contains("Compile")) {
                configurations.getByName((String) it).extendsFrom(configuration)
            } else if (isCompile && !it.contains("Implementation")) {
                configurations.getByName((String) it).extendsFrom(configuration)
            }
        }
    }
}

configurations {
    loneRequire
    require
    embed

    api
    apiImplementation.extendsFrom(api)

    compile
    apiCompileOnly.extendsFrom(compile)
}

setupConfiguration(configurations.require, false)
setupConfiguration(configurations.embed, false)
setupConfiguration(configurations.compile, true)
setupConfiguration2(configurations.loneRequire, false)

var apiVersion = "0.0.2-alpha"

var gdxVersion = "1.13.0"
var saveLibVersion = "244b61a8d9";
var puzzleVersion = "9276247"

var crClientUrl = "https://github.com/CRModders/CosmicArchive/raw/refs/heads/main/versions/pre-alpha/${cosmic_reach_client_version}/client/Cosmic%20Reach-${cosmic_reach_client_version}.jar"
var crServerUrl = "https://github.com/CRModders/CosmicArchive/raw/refs/heads/main/versions/pre-alpha/${cosmic_reach_client_version}/server/Cosmic%20Reach-Server-${cosmic_reach_client_version}.jar"

File libFolder = file("lib")
if (!libFolder.exists()) libFolder.mkdir()

File crFile = file("lib/cr${cosmic_reach_client_version}.jar")
if (!crFile.exists()) {
    try {
        new URL(crServerUrl).withInputStream { i -> crFile.withOutputStream { it << i}}
    } catch (Exception ignore) {
        new URL(crServerUrl.replaceAll("%20", "-")).withInputStream { i -> crFile.withOutputStream { it << i}}
    }
}

dependencies {
    compile 'org.jetbrains:annotations:24.0.0'

    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.github.FinalForEach:Cosmic-Reach-Save-Library:$saveLibVersion"

    require "com.github.PuzzlesHQ:PuzzleLoader:${puzzleVersion}:client"
    embed files("./lib/api.jar")
    require files(crFile)

    loneRequire sourceSets.commonBackend.output
}

processBackend_0_3_26Resources {
    // Locations of where to inject the properties
    def resourceTargets = [ "puzzle.mod.json" ]

    // Left item is the name in the target, right is the varuable name
    def replaceProperties = [
            "crVersion"     : cosmic_reach_client_version,
            "version"       : apiVersion
    ]

    inputs.properties replaceProperties
    replaceProperties.put "project", project
    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

compileApiJava {

}

tasks.register("buildAPI", Jar) {
    group = "buildSrc"
    from sourceSets.api.output

    doLast {
        GameScanner.scan(crFile)
        GenericTransformer.process(
                archiveFile.get().getAsFile(),
                new ClassPreProcessor(),
        )

        GenericTransformer.transform(
                archiveFile.get().getAsFile(),
                new DefaultClassTransformer(),
                new ApiClassTransformer(),
                new RedirectClassTransformer(),
                new RemoveClassTransformer(),
        )

        /* Rename & Move archive from /build/libs/api-x.y.z.jar to /lib/api.jar */
        File archive = new File(archiveFile.get().getAsFile().absolutePath);
        archive.renameTo(new File("$projectDir/lib/api.jar"))
    }
}

tasks.register("buildBackend-0.3.26", ShadowJar) {
    group = "buildSrc"
    configurations = [ project.configurations.embed ]

    archiveBaseName = "cosmic-api"
    archiveVersion = "v" + apiVersion + "_cr" + cosmic_reach_client_version

    from sourceSets.commonBackend.output
    from sourceSets.backend_0_3_26.output
    from processBackend_0_3_26Resources.outputs
}


